title Inverge
author metagameface
homepage www.puzzlescript.net

========
OBJECTS
========

BackgroundB
BLACK

BackgroundW
WHITE

Wall 
BROWN

Goal
RED
0...0
.0.0.
..0..
.0.0.
0...0


PlayerB
BLACK LIGHTGREEN
.000.
.010.
00100
.000.
.0.0.

InactivePlayerB
BLACK DARKGREEN
.000.
.010.
.010.
.000.
.0.0.

PlayerW
WHITE LIGHTGREEN
.000.
.010.
00100
.000.
.0.0.

InactivePlayerW
WHITE DARKGREEN
.000.
.010.
.010.
.000.
.0.0.

CrateB
DARKGREY BLACK 
00000
01100
01010
00110
00000

CrateW
LIGHTGRAY WHITE
00000
01100
01010
00110
00000

VertCrateTopB
DARKGREY BLACK 
00000
01100
01010
00110
01110

VertCrateBottomB
DARKGREY BLACK 
01110
01100
01010
00110
00000

VertCrateTopW
LIGHTGRAY WHITE
00000
01100
01010
00110
01110

VertCrateBottomW
LIGHTGRAY WHITE
01110
01100
01010
00110
00000

HorizCrateLeftB
DARKGREY BLACK 
00000
01101
01011
00111
00000

HorizCrateRightB
DARKGREY BLACK
00000
11100
11010
10110
00000

HorizCrateLeftW
LIGHTGRAY WHITE 
00000
01101
01011
00111
00000

HorizCrateRightW
LIGHTGRAY WHITE
00000
11100
11010
10110
00000

=======
LEGEND
=======

Player = PlayerB or PlayerW
InactivePlayer = InactivePlayerB or InactivePlayerW
Background = BackgroundB or BackgroundW or Wall
Crate = CrateB or CrateW
AnyPlayerB = PlayerB or InactivePlayerB
AnyPlayerW = PlayerW or InactivePlayerW
AnyPlayer = AnyPlayerB or AnyPlayerW
CratePieceB = CrateB or VertCrateTopB or VertCrateBottomB or HorizCrateLeftB or HorizCrateRightB
CratePieceW = CrateW or VertCrateTopW or VertCrateBottomW or HorizCrateLeftW or HorizCrateRightW
CratePiece = CratePieceB or CratePieceW

0 = BackgroundW
1 = BackgroundB
# = Wall
B = PlayerB and BackgroundW
W = InactivePlayerW and BackgroundB
+ = CrateB and BackgroundW
- = CrateW and BackgroundB
X = Goal and BackgroundW
Y = Goal and BackgroundB
p = VertCrateTopB and BackgroundW
d = VertCrateBottomB and BackgroundW
n = VertCrateTopW and BackgroundB
u = VertCrateBottomW and BackgroundB
{ = HorizCrateLeftB and BackgroundW
} = HorizCrateRightB and BackgroundW
6 = HorizCrateLeftW and BackgroundB
9 = HorizCrateRightW and BackgroundB


=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Goal
CratePiece
Player, InactivePlayer

======
RULES     
======

(Swap active player)
[Action PlayerB] [InactivePlayerW] -> [InactivePlayerB] [PlayerW]
[Action PlayerW] [InactivePlayerB] -> [InactivePlayerW] [PlayerB]

(Players can't move onto a background of the opposite colour unless there is a crate)
[> PlayerB | BackgroundB no CratePieceW] -> [PlayerB | BackgroundB]
[> PlayerW | BackgroundW no CratePieceB] -> [PlayerW | BackgroundW]

(Players can push crates of their colour)
[> PlayerB | CratePieceB] -> [> PlayerB | > CratePieceB ]
[> PlayerW | CratePieceW] -> [> PlayerW | > CratePieceW ]

(If one half of a 1x2 crate moves, so does the other half
These don't work properly right now, probably should use rigid body code
if I end up using them)

[perpendicular VertCrateTopB | VertCrateBottomB] -> [perpendicular VertCrateTopB | perpendicular VertCrateBottomB]
[perpendicular VertCrateBottomB | VertCrateTopB] -> [perpendicular VertCrateBottomB | perpendicular VertCrateTopB]
[parallel VertCrateTopB | VertCrateBottomB] -> [parallel VertCrateTopB | parallel VertCrateBottomB]
[parallel VertCrateBottomB | VertCrateTopB] -> [parallel VertCrateBottomB | parallel VertCrateTopB]
[perpendicular HorizCrateLeftB | HorizCrateRightB] -> [perpendicular HorizCrateLeftB | perpendicular HorizCrateRightB]
[perpendicular HorizCrateRightB | HorizCrateLeftB] -> [perpendicular HorizCrateRightB | perpendicular HorizCrateLeftB]
[parallel HorizCrateLeftB | HorizCrateRightB] -> [parallel HorizCrateLeftB | parallel HorizCrateRightB]
[parallel HorizCrateRightB | HorizCrateLeftB] -> [parallel HorizCrateRightB | parallel HorizCrateLeftB]

[perpendicular VertCrateTopW | VertCrateBottomW] -> [perpendicular VertCrateTopW | perpendicular VertCrateBottomW]
[perpendicular VertCrateBottomW | VertCrateTopW] -> [perpendicular VertCrateBottomW | perpendicular VertCrateTopW]
[parallel VertCrateTopW | VertCrateBottomW] -> [parallel VertCrateTopW | parallel VertCrateBottomW]
[parallel VertCrateBottomW | VertCrateTopW] -> [parallel VertCrateBottomW | parallel VertCrateTopW]
[perpendicular HorizCrateLeftW | HorizCrateRightW] -> [perpendicular HorizCrateLeftW | perpendicular HorizCrateRightW]
[perpendicular HorizCrateRightW | HorizCrateLeftW] -> [perpendicular HorizCrateRightW | perpendicular HorizCrateLeftW]
[parallel HorizCrateLeftW | HorizCrateRightW] -> [parallel HorizCrateLeftW | parallel HorizCrateRightW]
[parallel HorizCrateRightW | HorizCrateLeftW] -> [parallel HorizCrateRightW | parallel HorizCrateLeftW]

(Crates can't move onto a background of the same colour)
[> CratePieceB | BackgroundB] -> [CratePieceB | BackgroundB]
[> CratePieceW | BackgroundW] -> [CratePieceW | BackgroundW]

(Crates can't move into each other)
[> CratePiece | stationary CratePiece] -> [CratePiece | CratePiece]

(If a player is in a moving crate, they move too)
[> CratePiece AnyPlayer] -> [> CratePiece > AnyPlayer]

(Players and crates can't move into walls)
[> AnyPlayer | Wall] -> [AnyPlayer | Wall]
[> CratePiece | Wall] -> [CratePiece | Wall]

(Players can't move into crates of their own colour)
[> PlayerB | stationary CratePieceB] -> [PlayerB | CratePieceB]
[> PlayerW | stationary CratePieceW] -> [PlayerW | CratePieceW]

==============
WINCONDITIONS
==============

All Player On Goal
All InactivePlayer On Goal

=======     
LEVELS
=======

message Level 1

########
#000111#
#0b01w1#
#000111#
#0y111x#
#000000#
########

message Level 2

########
#b+01w1#
#101111#
#0+11--#
#0001--#
#1+11-1#
#0011-y#
#x011-1#
########

message Level 3

#######
#b0w00#
#00100#
#0+000#
#00100#
#1111##
#1-1+0#
#y110x#
#######

message Level 4

##########
#######11#
#####11-1#
#11111##1#
#10001##1#
#100+1##1#
#b0000001#
#11101000#
#1yx0w001#
##########

message Level 5

##########
#1w11110y#
#b+0000+0#
#001-1-11#
##111110##
#000+0000#
#111-1####
#x001#####
##########

message Level 6

#############
#b0000001y0x#
#00000001100#
#0+000001100#
#011001111-1#
#01w00111111#
#############

message placeholder Level 7

##########
######111#
#111111-1#
#11111##1#
#10001##1#
#100+1001#
#b0000001#
#11101101#
#1yx0w101#
##########

message placeholder Level 8

##########
#####1111#
#111111-1#
#111111#1#
#100011#1#
#100+1##1#
#b0000001#
#11101101#
#1yx0w101#
##########