title Inverge
author metagameface
homepage www.puzzlescript.net

========
OBJECTS
========

BackgroundB
BLACK

BackgroundW
WHITE

Wall 
BROWN

Goal
RED
0...0
.0.0.
..0..
.0.0.
0...0


PlayerB
BLACK LIGHTGREEN
.000.
.010.
00100
.000.
.0.0.

InactivePlayerB
BLACK DARKGREEN
.000.
.010.
.010.
.000.
.0.0.

PlayerW
WHITE LIGHTGREEN
.000.
.010.
00100
.000.
.0.0.

InactivePlayerW
WHITE DARKGREEN
.000.
.010.
.010.
.000.
.0.0.

CrateB
DARKGREY BLACK 
00000
01100
01010
00110
00000

CrateW
LIGHTGRAY WHITE
00000
01100
01010
00110
00000

=======
LEGEND
=======

Player = PlayerB or PlayerW
InactivePlayer = InactivePlayerB or InactivePlayerW
Background = BackgroundB or BackgroundW or Wall
Crate = CrateB or CrateW
AnyPlayerB = PlayerB or InactivePlayerB
AnyPlayerW = PlayerB or InactivePlayerW
AnyPlayer = AnyPlayerB or AnyPlayerW

0 = BackgroundW
1 = BackgroundB
# = Wall
B = PlayerB and BackgroundW
W = InactivePlayerW and BackgroundB
+ = CrateB and BackgroundW
- = CrateW and BackgroundB
X = Goal and BackgroundW
Y = Goal and BackgroundB


=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Goal
Crate
Player, InactivePlayer

======
RULES     
======

(Swap active player)
[Action PlayerB] [InactivePlayerW] -> [InactivePlayerB] [PlayerW]
[Action PlayerW] [InactivePlayerB] -> [InactivePlayerW] [PlayerB]

(Players can't move onto a background of the opposite colour unless there is a crate)
[> PlayerB | BackgroundB no Crate] -> [PlayerB | BackgroundB]
[> PlayerW | BackgroundW no Crate] -> [PlayerW | BackgroundW]

(Push crates of your colour)
[> PlayerB | CrateB] -> [> PlayerB | > CrateB ]
[> PlayerW | CrateW] -> [> PlayerW | > CrateW ]

(Crates can't move onto a background of the opposite colour)
[> CrateB | BackgroundB] -> [CrateB | BackgroundB]
[> CrateW | BackgroundW] -> [CrateW | BackgroundW]

(Crates can't move into each other)
[> Crate | Crate] -> [Crate | Crate]

(If a player is in a moving crate, they move too)
[> Crate AnyPlayer] -> [> Crate > AnyPlayer]

(Players and crates can't move into walls)
[> Player | Wall] -> [Player | Wall]
[> Crate | Wall] -> [Crate | Wall]

(Players can't move into crates of their own colour)
[> PlayerB | stationary CrateB] -> [PlayerB | CrateB]
[> PlayerW | stationary CrateW] -> [PlayerW | CrateW]

==============
WINCONDITIONS
==============

All Player On Goal
All InactivePlayer On Goal

=======     
LEVELS
=======

########
#000111#
#0b01w1#
#000111#
#00011x#
#0y1110#
#000000#
########

########
#b+01w1#
#101111#
#0+11--#
#0001--#
#1+11-1#
#0011-y#
#x011-1#
########

#######
#B0W00#
#00100#
#0+000#
#00100#
#11111#
#1-100#
#Y110X#
#######

##########
#####1111#
#111-11-1#
#11111##1#
#10001##1#
#100+1##1#
#b0000001#
#11101001#
#1yx0w001#
##########

#############
#B0000001Y0X#
#00000001100#
#0+000001100#
#011001111-1#
#01W00111111#
#############

##########
######111#
#111111-1#
#11111##1#
#10001##1#
#100+1001#
#b0000001#
#11101101#
#1yx0w101#
##########

##########
#####1111#
#111111-1#
#111111#1#
#100011#1#
#100+1##1#
#b0000001#
#11101101#
#1yx0w101#
##########